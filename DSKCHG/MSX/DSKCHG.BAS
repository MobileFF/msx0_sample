1 'SAVE"DSKCHG.bas"
1000 'CREATE DSK FILES LIST
1010 CLEAR 2000:DIM N$(255)
1020 ON ERROR GOTO 8000
1030 KEY9,"RUN"+CHR$(34)+"DSKCHG.BAS"+CHR$(13)
1040 SCREEN 0:WIDTH 40
1050 _IOTFIND("host/media/dsk",C)
1060 _IOTFIND("host/media/dsk",N$(0),C)

1500 'PRINT DSK FILES
1510 _IOTGET("msx/u0/drive/a",CR$)
1520 PRINT "DSK FILES [CURRENT:"+CR$+"]"
1530 CL=0
1540 FOR I=0 TO C-1
1550   PRINT LEFT$(N$(I)+"            ",18);
1560   IF CL<1 THEN PRINT " ";
1570   CL=CL+1:IF CL>1 THEN CL=0:PRINT
1580 NEXT I

2000 'SELECT FILE
2010 L$=STRING$(19,29):R$=STRING$(19,28)
2020 LOCATE ,,1
2100 A$=INKEY$
2110 X=POS(0):Y=CSRLIN
2120 IF A$=CHR$(13) THEN GOTO 3000
2130 IF A$=CHR$(29) THEN A$=L$
2140 IF A$=CHR$(28) THEN A$=R$
2150 IF A$>CHR$(27) AND A$<" " THEN PRINT A$;
2990 GOTO 2100

3000 'CHANGE DSK
3010 II=Y*40+X+1:LOCATE ,,0:PRINT CHR$(11);
3020 FOR I=II TO II+11
3030   X$=X$+CHR$(VPEEK(I))
3040 NEXT I
3050 CLS
3060 _IOTPUT("msx/u0/drive/a",X$)
3070 _IOTGET("msx/u0/drive/a",Y$)
3080 _IOTFIND("host/media/dsk",C)
3090 _IOTFIND("host/media/dsk",N$(0),C)
3100 FILES "XXXXXXXX.XXX"

8000 'PROGRAM END
8010 PRINT "DSK Changed:";X$
8020 END
