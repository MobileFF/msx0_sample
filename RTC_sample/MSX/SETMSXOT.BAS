1 'SAVE"SETMSXOT.BAS"
100 'Init
110   N0$="device/i2c_i"
120   BM$="51"
130   NP$=N0$+"/"+BM$
1000 'Find I2C Devices
1100   PRINT "BM8563(RTC) ";
1110   _IOTFIND(N0$,C)
1120   IF C=0 THEN PRINT "Not Found.":END
1200   _IOTFIND(N0$,A$(0),C)
1210   FOR I=0 TO C-1
1220     IF A$(I)=BM$ THEN PRINT "Found.":GOTO 2000
1230   NEXT I
1900   PRINT "Not Found.":END
2000 'BM8563 Initialize
2100   _IOTPUT(NP$,CHR$(&H0)+CHR$(&H0))
2110   _IOTPUT(NP$,CHR$(&H1)+CHR$(&H0))
2120   _IOTPUT(NP$,CHR$(&HD)+CHR$(&H0))
3000   YE=1983 :'Year
3010   MO=6    :'Month
3020   DW=4    :'Thursday
3030   DA=16   :'Date
3040   HO=0   :'Hour
3050   MI=0   :'Minute
3060   SE=0   :'Second
3100   GET DATE MD$
3110   GET TIME MT$
3200   Y1 = VAL("20"+MID$(MD$,1,2))-YE
3210   M2 = VAL(MID$(MD$,4,2))-MO+1
3230   IF M2<0 THEN M2=12+M2:Y1=Y1-1
3240   D3 = 0
3250   D4 = VAL(MID$(MD$,7,2))-DA+1
3260   IF D4<0 THEN D4=30+D4:M2=M2-1:IF M2=0 THEN M2=12:Y1=Y1-1
3270   H5 = VAL(MID$(MT$,1,2))
3280   M6 = VAL(MID$(MT$,4,2))
3290   S7 = VAL(MID$(MT$,7,2))

4100 'Year
4110   CT$="0"
4130   V$=STR$(Y1):V$=RIGHT$(V$,LEN(V$)-1)
4140   DT$=CHR$(VAL("&H"+V$))
4195 PRINT HEX$(ASC(DT$))
4200 'Month
4210   B4$="0"
4220   IF M2>=10 THEN B4$="1":M2=M2-10
4230   DT$=CHR$(VAL("&B"+CT$+"00"+B4$+"0000")+M2)+DT$
4235 PRINT HEX$(ASC(DT$))
4300 'Day of Week
4310   DT$=CHR$(D3)+DT$
4335 PRINT HEX$(ASC(DT$))
4400 'Date
4410   V$=STR$(D4):V$=RIGHT$(V$,LEN(V$)-1)
4420   DT$=CHR$(VAL("&H"+V$))+DT$
4435 PRINT HEX$(ASC(DT$))
4600 'Hour
4610   V$=STR$(H5):V$=RIGHT$(V$,LEN(V$)-1)
4620   DT$=CHR$(VAL("&H"+V$))+DT$
4630 PRINT HEX$(ASC(DT$))
4700 'Minute
4710   V$=STR$(M6):V$=RIGHT$(V$,LEN(V$)-1)
4720   DT$=CHR$(VAL("&H"+V$))+DT$
4735 PRINTHEX$(ASC(DT$))
4800 'Second
4810   V$=STR$(S7):V$=RIGHT$(V$,LEN(V$)-1)
4820   DT$=CHR$(VAL("&H"+V$))+DT$
4835 PRINT HEX$(ASC(DT$))

4900 FOR I=1 TO 7
4910 PRINT RIGHT$("0"+HEX$(ASC(MID$(DT$,I,1))),2);" ";
4920 NEXT I
4930 PRINT
5000 'Set Date
5010   PRINT "RTC Set To ";Y1;"/";M2;"/";D4;" ";H5;":";M6;":";S7;"..."
5100   _IOTPUT(NP$,CHR$(&H2)+DT$)

SAVE"SETMSXOT.BAS"

